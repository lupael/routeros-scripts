#!rsc by RouterOS
# RouterOS script: gps-track
# Copyright (c) 2018-2021 Christian Hesse <mail@eworm.de>
# https://git.eworm.de/cgit/routeros-scripts/about/COPYING.md
#
# track gps data by sending json data to http server
# https://git.eworm.de/cgit/routeros-scripts/about/doc/gps-track.md

:local 0 "gps-track";
:global GlobalFunctionsReady;
:while ($GlobalFunctionsReady != true) do={ :delay 500ms; }

:global GpsTrackUrl;
:global Identity;

:global LogPrintExit2;

:local CoordinateFormat [ / system gps get coordinate-format ];
:local Gps [ / system gps monitor once as-value ];

:if ($Gps->"valid" = true) do={
  / tool fetch check-certificate=yes-without-crl $GpsTrackUrl output=none \
    http-method=post http-header-field="Content-Type: application/json" \
    http-data=("{" . \
      "\"lat\":\"" . ($Gps->"latitude") . "\"," . \
      "\"lon\":\"" . ($Gps->"longitude") . "\"," . \
      "\"identity\":\"" . $Identity . "\"" . \
    "}") as-value;
  $LogPrintExit2 debug $0 ("Sending GPS data in " . $CoordinateFormat . " format: " . \
    "lat: " . ($Gps->"latitude") . " " . \
    "lon: " . ($Gps->"longitude")) false;
} else={
  $LogPrintExit2 debug $0 ("GPS data not valid.") false;
}
